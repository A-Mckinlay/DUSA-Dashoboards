// Dashoboards build file
//
// In case of issues, please report in Slack!
//

buildscript {
    repositories {
        mavenCentral()
        // The following is only necessary if you want to use SNAPSHOT releases.
        // maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
    }
}

plugins {
    id 'application'
    id 'idea'
    id 'eclipse'
    id 'com.github.johnrengelman.shadow' version '2.0.1'
    id 'com.moowork.grunt' version '1.2.0'
}

apply plugin: 'org.junit.platform.gradle.plugin'

repositories {
    mavenCentral()
    maven { // Sonatype Snapshots (JUnit 5)
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
    maven {
        name 'Tethys'
        url 'http://tethys.drakon.io/maven'
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
group = 'uk.ac.dundee.ac41004.team9'
mainClassName = 'uk.ac.dundee.ac41004.team9.Main'
version = '0.0.1'

grunt_build.dependsOn += npmInstall
grunt_build.dependsOn += installGrunt

// Put Grunt before compile, so we're before Gradle searches for files for packaging.
compileJava.dependsOn += grunt_build

build.dependsOn += shadowJar

dependencies {
    // Spark
    compile group: 'com.sparkjava', name: 'spark-core', version: '2.6.0'
    compile group: 'com.sparkjava', name: 'spark-template-mustache', version: '2.5.5'

    // Logging (SLF4j + Logback Classic)
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

    // Autorouter
    compile group: 'io.drakon.spark', name: 'autorouter', version: '0.0.6'
    compile group: 'io.drakon.spark', name: 'syn', version: '0.0.5'

    // Excel
    compile group: 'org.apache.poi', name: 'poi', version: '3.17'
    compile group: 'org.apache.poi', name: 'poi-ooxml', version: '3.17'

    // Misc. bits
    compile group: 'org.postgresql', name: 'postgresql', version: '42.1.4' // DB driver
    compile group: 'org.projectlombok', name: 'lombok', version: '1.16.18' // Lombok!
    compile group: 'com.google.guava', name: 'guava', version: '23.0' // Guava utilities
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2' // JSR305 (@Nullable annotations)
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.1' // JSON handling
    compile group: 'org.jooq', name: 'jool', version: '0.9.12' // Lambda utils

    // JUnit
    testRuntime group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.0.0'
    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.0.0'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.0.0'
    testCompile group: 'org.junit.vintage', name: 'junit-vintage-engine', version: '4.12.0'
}